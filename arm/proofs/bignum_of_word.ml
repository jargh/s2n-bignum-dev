(*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0 OR ISC OR MIT-0
 *)

(* ========================================================================= *)
(* Conversion of a single word (digit) to a bignum.                          *)
(* ========================================================================= *)

needs "arm/proofs/base.ml";;

let bignum_of_word_mc =
  define_assert_from_elf "bignum_of_word_mc" "arm/generic/bignum_of_word.o"
[
  0xb40000e0;       (* arm_CBZ X0 (word 28) *)
  0xf9000022;       (* arm_STR X2 X1 (Immediate_Offset (word 0)) *)
  0xf1000400;       (* arm_SUBS X0 X0 (rvalue (word 1)) *)
  0x54000080;       (* arm_BEQ (word 16) *)
  0xf820783f;       (* arm_STR XZR X1 (Shiftreg_Offset X0 3) *)
  0xf1000400;       (* arm_SUBS X0 X0 (rvalue (word 1)) *)
  0x54ffffc1;       (* arm_BNE (word 2097144) *)
  0xd65f03c0        (* arm_RET X30 *)
];;

let BIGNUM_OF_WORD_EXEC = ARM_MK_EXEC_RULE bignum_of_word_mc;;

(* ------------------------------------------------------------------------- *)
(* Correctness proof.                                                        *)
(* ------------------------------------------------------------------------- *)

let BIGNUM_OF_WORD_CORRECT = prove
 (`!k z n pc.
        nonoverlapping (word pc,0x20) (z,8 * val k)
        ==> ensures arm
             (\s. aligned_bytes_loaded s (word pc) bignum_of_word_mc /\
                  read PC s = word pc /\
                  C_ARGUMENTS [k; z; n] s)
             (\s. read PC s = word (pc + 0x1c) /\
                  bignum_from_memory (z,val k) s =
                  val n MOD (2 EXP (64 * val k)))
         (MAYCHANGE [PC; X0; X2] ,, MAYCHANGE SOME_FLAGS ,, MAYCHANGE [events] ,,
          MAYCHANGE [memory :> bignum(z,val k)])`,
  W64_GEN_TAC `k:num` THEN X_GEN_TAC `z:int64` THEN
  W64_GEN_TAC `n:num` THEN X_GEN_TAC `pc:num` THEN
  REWRITE_TAC[NONOVERLAPPING_CLAUSES] THEN
  REWRITE_TAC[C_ARGUMENTS; C_RETURN; SOME_FLAGS] THEN DISCH_TAC THEN

  ASM_CASES_TAC `k = 0` THENL
   [ASM_REWRITE_TAC[BIGNUM_FROM_MEMORY_TRIVIAL] THEN
    ARM_SIM_TAC BIGNUM_OF_WORD_EXEC [1] THEN
    REWRITE_TAC[MULT_CLAUSES; EXP; MOD_1];
    ALL_TAC] THEN

  ASM_CASES_TAC `k = 1` THENL
   [ARM_SIM_TAC BIGNUM_OF_WORD_EXEC (1--4) THEN
    REWRITE_TAC[GSYM BIGNUM_FROM_MEMORY_BYTES] THEN
    ASM_SIMP_TAC[BIGNUM_FROM_MEMORY_SING; MULT_CLAUSES; MOD_LT];
    ALL_TAC] THEN

  FIRST_ASSUM(MP_TAC o MATCH_MP (ONCE_REWRITE_RULE[IMP_CONJ]
        NONOVERLAPPING_IMP_SMALL_2)) THEN
  ANTS_TAC THENL [SIMPLE_ARITH_TAC; DISCH_TAC] THEN

  ENSURES_WHILE_PADOWN_TAC `k:num` `1` `pc + 0x10` `pc + 0x18`
   `\i s. (read X1 s = z /\
           read X0 s = word(i - 1) /\
           read (memory :> bytes64 z) s = word n /\
           bignum_from_memory(word_add z (word(8 * i)),k - i) s = 0) /\
          (read ZF s <=> i = 1)` THEN
  ASM_REWRITE_TAC[] THEN REPEAT CONJ_TAC THENL
   [ASM_REWRITE_TAC[ARITH_RULE `1 < k <=> ~(k = 0) /\ ~(k = 1)`];
    MP_TAC(ISPECL [`word k:int64`; `word 1:int64`] VAL_WORD_SUB_EQ_0) THEN
    ASM_REWRITE_TAC[VAL_WORD_1] THEN DISCH_TAC THEN
    ARM_SIM_TAC BIGNUM_OF_WORD_EXEC (1--4) THEN
    REWRITE_TAC[GSYM BIGNUM_FROM_MEMORY_BYTES] THEN
    ASM_SIMP_TAC[BIGNUM_FROM_MEMORY_TRIVIAL; WORD_SUB; LE_1];
    X_GEN_TAC `i:num` THEN STRIP_TAC THEN
    VAL_INT64_TAC `i:num` THEN REWRITE_TAC[ADD_SUB] THEN
    ARM_SIM_TAC BIGNUM_OF_WORD_EXEC (1--2) THEN
    ASM_SIMP_TAC[WORD_SUB; LE_1; VAL_WORD_1] THEN
    REWRITE_TAC[GSYM BIGNUM_FROM_MEMORY_BYTES] THEN
    ONCE_REWRITE_TAC[BIGNUM_FROM_MEMORY_EXPAND] THEN
    ASM_REWRITE_TAC[VAL_WORD_0; ADD_CLAUSES; SUB_EQ_0; GSYM NOT_LT] THEN
    REWRITE_TAC[WORD_RULE
     `word_add (word_add z (word (8 * i))) (word 8) =
      word_add z (word (8 * (i + 1)))`] THEN
    REWRITE_TAC[ARITH_RULE `k - i - 1 = k - (i + 1)`] THEN
    ASM_REWRITE_TAC[BIGNUM_FROM_MEMORY_BYTES; MULT_CLAUSES];
    REPEAT STRIP_TAC THEN ARM_SIM_TAC BIGNUM_OF_WORD_EXEC [1];
    ARM_SIM_TAC BIGNUM_OF_WORD_EXEC [1] THEN
    RULE_ASSUM_TAC(REWRITE_RULE[MULT_CLAUSES]) THEN
    REWRITE_TAC[GSYM BIGNUM_FROM_MEMORY_BYTES] THEN
    ONCE_REWRITE_TAC[BIGNUM_FROM_MEMORY_EXPAND] THEN
    ASM_REWRITE_TAC[BIGNUM_FROM_MEMORY_BYTES; ADD_CLAUSES; MULT_CLAUSES] THEN
    CONV_TAC SYM_CONV THEN MATCH_MP_TAC MOD_LT THEN
    TRANS_TAC LTE_TRANS `2 EXP 64` THEN ASM_REWRITE_TAC[LE_EXP] THEN
    UNDISCH_TAC `~(k = 0)` THEN ARITH_TAC]);;

let BIGNUM_OF_WORD_SUBROUTINE_CORRECT = prove
 (`!k z n pc returnaddress.
        nonoverlapping (word pc,0x20) (z,8 * val k)
        ==> ensures arm
             (\s. aligned_bytes_loaded s (word pc) bignum_of_word_mc /\
                  read PC s = word pc /\
                  read X30 s = returnaddress /\
                  C_ARGUMENTS [k; z; n] s)
             (\s. read PC s = returnaddress /\
                  bignum_from_memory (z,val k) s =
                  val n MOD (2 EXP (64 * val k)))
             (MAYCHANGE_REGS_AND_FLAGS_PERMITTED_BY_ABI ,,
              MAYCHANGE [memory :> bignum(z,val k)])`,
  ARM_ADD_RETURN_NOSTACK_TAC BIGNUM_OF_WORD_EXEC BIGNUM_OF_WORD_CORRECT);;

(* ------------------------------------------------------------------------- *)
(* Constant-time and memory safety proof.                                    *)
(* ------------------------------------------------------------------------- *)

needs "arm/proofs/consttime.ml";;
needs "arm/proofs/subroutine_signatures.ml";;

let full_spec,public_vars = mk_safety_spec
    ~keep_maychanges:false
    (assoc "bignum_of_word" subroutine_signatures)
    BIGNUM_OF_WORD_SUBROUTINE_CORRECT
    BIGNUM_OF_WORD_EXEC;;

let BIGNUM_OF_WORD_SUBROUTINE_SAFE = time prove
 (`exists f_events.
       forall e k z n pc returnaddress.
           nonoverlapping (word pc,32) (z,8 * val k)
           ==> ensures arm
               (\s.
                    aligned_bytes_loaded s (word pc) bignum_of_word_mc /\
                    read PC s = word pc /\
                    read X30 s = returnaddress /\
                    C_ARGUMENTS [k; z; n] s /\
                    read events s = e)
               (\s.
                    read PC s = returnaddress /\
                    (exists e2.
                         read events s = APPEND e2 e /\
                         e2 = f_events z k pc returnaddress /\
                         memaccess_inbounds e2 [z,val k * 8] [z,val k * 8]))
               (\s s'. true)`,
  ASSERT_CONCL_TAC full_spec THEN
  CONCRETIZE_F_EVENTS_TAC
   `\(z:int64) (k:int64) (pc:num) (returnaddress:int64).
      if val k = 0 then f_ev_k0 z k pc returnaddress
      else if val k = 1 then f_ev_k1 z k pc returnaddress
      else APPEND (f_ev_post z k pc returnaddress)
             (APPEND (ENUMERATEL (val k - 1) (f_ev_loop z k pc returnaddress))
                     (f_ev_pre z k pc returnaddress)):(uarch_event) list` THEN
  REPEAT META_EXISTS_TAC THEN STRIP_TAC THEN
  REWRITE_TAC[C_ARGUMENTS; C_RETURN; SOME_FLAGS] THEN REPEAT STRIP_TAC THEN
  ABBREV_TAC `k' = val (k:int64)` THEN
  SUBGOAL_THEN `k' < 2 EXP 64` ASSUME_TAC THENL [
    EXPAND_TAC "k'" THEN MATCH_ACCEPT_TAC VAL_BOUND_64; ALL_TAC ] THEN
  ASM_CASES_TAC `k' = 0` THENL [
   ASM_REWRITE_TAC[] THEN
   ARM_SIM_TAC ~preprocess_tac:(TRY STRIP_EXISTS_ASSUM_TAC) ~canonicalize_pc_diff:false
     BIGNUM_OF_WORD_EXEC (1--2) THEN DISCHARGE_SAFETY_PROPERTY_TAC; ALL_TAC ] THEN
  ASM_CASES_TAC `k' = 1` THENL [
   ASM_REWRITE_TAC[] THEN SUBST1_TAC (ISPEC `k:int64` (GSYM WORD_VAL)) THEN
   ARM_SIM_TAC ~preprocess_tac:(TRY STRIP_EXISTS_ASSUM_TAC) ~canonicalize_pc_diff:false
     BIGNUM_OF_WORD_EXEC (1--5) THEN DISCHARGE_SAFETY_PROPERTY_TAC; ALL_TAC ] THEN
  ASM_REWRITE_TAC[] THEN
  ENSURES_EVENTS_WHILE_UP2_TAC `k' - 1` `pc + 0x10` `pc + 0x1c`
   `\i s. read X1 s = z /\ read X0 s = word((k' - 1) - i) /\ read X30 s = returnaddress` THEN
  ASM_REWRITE_TAC[] THEN
  CONJ_TAC THENL [SIMPLE_ARITH_TAC; ALL_TAC] THEN
  CONJ_TAC THENL [
    (* prologue: CBZ (not taken), pre-store, SUBS, BEQ (not taken) -> pc+16 *)
    SUBGOAL_THEN `~(val(word_sub k (word 1):int64) = 0)` ASSUME_TAC THENL [
      REWRITE_TAC[VAL_WORD_SUB_EQ_0; VAL_WORD_1] THEN ASM_REWRITE_TAC[]; ALL_TAC] THEN
    ARM_SIM_TAC ~preprocess_tac:(TRY STRIP_EXISTS_ASSUM_TAC) ~canonicalize_pc_diff:false
      BIGNUM_OF_WORD_EXEC (1--4) THEN
    CONJ_TAC THENL [
      REWRITE_TAC[SUB_0] THEN SUBST1_TAC (ISPEC `k:int64` (GSYM WORD_VAL)) THEN
      ASM_REWRITE_TAC[] THEN REWRITE_TAC[GSYM VAL_WORD_1] THEN
      ASM_SIMP_TAC[WORD_SUB; LE_1] THEN REWRITE_TAC[VAL_WORD_1]; ALL_TAC] THEN
    DISCHARGE_SAFETY_PROPERTY_TAC; ALL_TAC ] THEN
  CONJ_TAC THENL [
    (* loop body: store z[k'-1-i], decrement, branch *)
    REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[] THEN
    ENSURES_INIT_TAC "s0" THEN STRIP_EXISTS_ASSUM_TAC THEN
    ARM_STEPS_TAC BIGNUM_OF_WORD_EXEC (1--3) THEN
    ENSURES_FINAL_STATE_TAC THEN ASM_REWRITE_TAC[] THEN
    CONJ_TAC THENL [
      REWRITE_TAC[VAL_WORD_SUB_EQ_0] THEN
      IMP_REWRITE_TAC[VAL_WORD;DIMINDEX_64;MOD_LT] THEN
      REPEAT CONJ_TAC THENL [
        GEN_REWRITE_TAC RAND_CONV [COND_RAND] THEN SIMPLE_ARITH_TAC;
        SIMPLE_ARITH_TAC;
        SIMPLE_ARITH_TAC ];
      ALL_TAC ] THEN
    CONJ_TAC THENL [
      IMP_REWRITE_TAC[WORD_SUB2] THEN CONJ_TAC THENL
      [ AP_TERM_TAC THEN SIMPLE_ARITH_TAC; SIMPLE_ARITH_TAC ];
      ALL_TAC ] THEN
    SUBST1_TAC (ISPEC `k:int64` (GSYM WORD_VAL)) THEN
    REWRITE_TAC[ASSUME `val (k:int64) = k'`] THEN
    SUBGOAL_THEN `word_sub (word (k' - 1 - i)) (word 1) =
                  word (k' - 1 - (i + 1)):int64` SUBST_ALL_TAC THENL [
      IMP_REWRITE_TAC[WORD_SUB2] THEN
      CONJ_TAC THENL [ AP_TERM_TAC THEN SIMPLE_ARITH_TAC; SIMPLE_ARITH_TAC];
      ALL_TAC ] THEN
    MAP_EVERY VAL_INT64_TAC [`k':num`; `k'-1`] THEN
    DISCHARGE_SAFETY_PROPERTY_TAC; ALL_TAC ] THEN
  (* exit: RET *)
  ENSURES_INIT_TAC "s0" THEN STRIP_EXISTS_ASSUM_TAC THEN
  ARM_STEPS_TAC BIGNUM_OF_WORD_EXEC (1--1) THEN
  ENSURES_FINAL_STATE_TAC THEN ASM_REWRITE_TAC[] THEN
  DISCHARGE_SAFETY_PROPERTY_TAC);;
